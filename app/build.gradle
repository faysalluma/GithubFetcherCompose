apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: "dagger.hilt.android.plugin"
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-parcelize' // Parcelable


def vcode = (int) (((new Date().getTime() / 1000) - 1451606400) / 10)

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.groupec.githubfetchercompose"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode vcode
        versionName "1.1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file("local.properties").newDataInputStream())
            storeFile file(properties.getProperty('RELEASE_STORE_FILE'))
            storePassword properties.getProperty('RELEASE_STORE_PASSWORD')
            keyAlias properties.getProperty('RELEASE_KEY_ALIAS')
            keyPassword properties.getProperty('RELEASE_KEY_PASSWORD')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "GithubFetcher-Compose-${variant.name}-${variant.versionName}.apk"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    kapt {
        correctErrorTypes true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {

    // Core
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.20"

    // MVVM
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'

    // UI
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.compose.ui:ui:1.3.2"
    implementation "androidx.compose.material:material:1.3.1"
    implementation "androidx.compose.material3:material3:1.1.1"
    implementation "androidx.compose.material:material-icons-extended:1.3.1"
    implementation "androidx.compose.ui:ui-tooling-preview:1.3.2"
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    implementation 'androidx.compose.runtime:runtime-livedata:1.4.0-alpha03'
    debugImplementation "androidx.compose.ui:ui-tooling:1.3.2"
    debugImplementation "androidx.compose.ui:ui-test-manifest:1.3.2"

    // Navigation
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'

    // Room
    implementation "androidx.room:room-runtime:2.4.3"
    kapt "androidx.room:room-compiler:2.4.3"
    implementation "androidx.room:room-ktx:2.4.3"

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'

    // Hilt
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-compiler:2.44"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"

    // DataStore
    implementation 'androidx.datastore:datastore-preferences:1.0.0'

    // TabLayout with ViewPager
    implementation "com.google.accompanist:accompanist-pager:0.13.0"
    implementation "com.google.accompanist:accompanist-pager-indicators:0.13.0"

    //To display Images from URL
    implementation "io.coil-kt:coil-compose:2.4.0"

    // Unit Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}